000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. DB2PART1.
000300
000400*=============================================================
000500* PROGRAMME DB2PART1
000600* OBJECTIF : LIRE UN FICHIER SEQUENTIEL OUTPRODS CONTENANT
000700*            DES PRODUITS ET ALIMENTER LA TABLE DB2
000800*            API7.PRODUCTS (P_NO, DESCRIPTION, PRICE, STOCK).
000900*=============================================================
001000
001100
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 SPECIAL-NAMES.
001500      DECIMAL-POINT IS COMMA.
001600
001700 INPUT-OUTPUT SECTION.
001800 FILE-CONTROL.
001900
002000* FICHIER SEQUENTIEL D'ENTREE OUTPRODS
002100
002200     SELECT OUTPRODS ASSIGN TO FOP
002300     ORGANIZATION IS SEQUENTIAL
002400     FILE STATUS IS FS-OP.
002500
002600 DATA DIVISION.
002700 FILE SECTION.
002800
002900* DECLARATION DU FICHIER D'ENTREE
003000
003100 FD  OUTPRODS
003200     RECORDING MODE IS F.
003300 01  ENR-OP.
003400     05 CODE-OP          PIC X(3).
003500     05 DESC-OP          PIC X(30).
003600     05 PU-OP            PIC X(6).
003700     05 DE-OP            PIC X(3).
003800     05 ST-OP            PIC X(3).
003900
004000
004100
004200 WORKING-STORAGE SECTION.
004300
004400* ZONE DE COMMUNICATION SQL STANDARD (SQLCA)
004500
004600
004700     EXEC SQL
004800       INCLUDE SQLCA
004900     END-EXEC.
005000
005100     EXEC SQL  INCLUDE PRODUCTS  END-EXEC.
005200
005300
005400
005500*** VARIABLES FILE STATUS ***
005600
005700
005800 77 FS-OP          PIC 99.
005900
006000 77 FIN-PROD       PIC X VALUE 'N'.
006100 77 WS-FLAG-PROD   PIC 9 VALUE ZERO.
006200    88 FF-PROD     VALUE 1.
006300
006400***  VARIABLES ANOMALIES SQL  ***
006500
006600 77 WS-ANO         PIC 99 VALUE ZERO.
006700 77 WS-MSG-SQL     PIC X(30).
006800
006900 77 ED-SQLCODE     PIC +9(9).
007000
007100*** VARIABLES DE LOG ***
007200
007300 01  WS-MSG          PIC X(164).
007400 01  WS-PGM          PIC X(8) VALUE 'DB2PART1'.
007500 01  WS-CONTEXT      PIC X(30).
007600 01  WS-CONTEXT-LEN  PIC 99.
007700 01  WS-USER         PIC X(8).
007800
007900*** VARIABLES DE TRAVAIL PRIX ***
008000
008100 01 WS-PU-STR          PIC X(6).
008200 01 WS-PU-NUM          PIC 9(3)V9(2).
008300
008400
008500*** VARIABLES DE TRAVAIL STOCK ***
008600
008700 01 WS-STOCK-STR       PIC X(3).
008800 01 WS-STOCK-NUM       PIC 9(3).
008900
009000
009100 PROCEDURE DIVISION.
009200
009300     PERFORM OUVERTURE-FICHIERS
009400
009500     PERFORM UNTIL FIN-PROD = 'O'
009600              PERFORM LECTURE-OP
009700     END-PERFORM
009800
009900     CLOSE OUTPRODS
010000
010100     EXEC SQL    COMMIT        END-EXEC.
010200
010300*    VALIDATION GLOBALE DES INSERTIONS EN BASE
010400
010500     PERFORM TEST-SQLCODE
010600
010700*    VERIFICATION FINALE DU DERNIERE SQLCODE
010800
010900     GOBACK.
011000
011100 OUVERTURE-FICHIERS.
011200
011300*    OUVERTURE DU FICHIER OUTPRODS EN LECTURE SEULE
011400
011500     OPEN INPUT OUTPRODS
011600      IF FS-OP NOT = ZEROES THEN
011700
011800*    EN CAS D'ANOMALIE, AFFICHAGE DU STATUT
011900
012000        DISPLAY 'ERREUR OUVERTURE OUTPRODS : ' FS-OP
012100      END-IF
012200      .
012300
012400 LECTURE-OP.
012500
012600*    LECTURE DU FICHIER OUTPRODS
012700
012800
012900     READ OUTPRODS
013000      AT END
013100
013200*    SI FIN DE FICHIER, ON SORT DE LA BOUCLE
013300
013400        MOVE 'O' TO FIN-PROD
013500      NOT AT END
013600
013700*    SI LECTURE EN COURS, CONVERSION DES CHAMPS TEXTE EN NUMERIQUE
013800*    PUIS INSERTION DANS LA TABLE PRODUCTS
013900
014000        MOVE 'N' TO FIN-PROD
014100        PERFORM CONVERSIONS-VAR
014200        PERFORM AJOUT-OP
014300     END-READ
014400
014500
014600     .
014700
014800 TRANSFERT.
014900
015000* ALIMENTATION DES VARIABLES HOTES (DCLGEN)
015100
015200
015300     MOVE CODE-OP              TO PRO-P-NO
015400     MOVE DESC-OP              TO PRO-DESCRIPTION-TEXT
015500     MOVE LENGTH OF DESC-OP    TO PRO-DESCRIPTION-LEN
015600     MOVE WS-PU-NUM            TO PRO-PRICE
015700     MOVE WS-STOCK-NUM         TO PRO-STOCK
015800
015900     .
016000
016100 CONVERSIONS-VAR.
016200
016300*** CONVERSION PU ***
016400
016500*    COPIE DE LA ZONE PRIX DU FICHIER VERS VAR DE TRAVAIL
016600
016700     MOVE PU-OP TO WS-PU-STR
016800
016900*    REMPLACEMENT DES POINTS PAR LES VIRGULES (SEPARATEUR DEC)
017000*    ET CONVERSION DE LA VAR DE TRAVAIL EN NUMERIQUE
017100
017200     INSPECT WS-PU-STR REPLACING ALL '.' BY ','
017300     COMPUTE WS-PU-NUM = FUNCTION NUMVAL(WS-PU-STR)
017400     DISPLAY 'FORMAT WS-PU-NUM : ' WS-PU-NUM
017500     DISPLAY 'VALEUR DE WS-PU-NUM : [' WS-PU-NUM ']'
017600
017700*** CONVERSION STOCK ***
017800
017900     MOVE ST-OP TO WS-STOCK-STR
018000     COMPUTE WS-STOCK-NUM = FUNCTION NUMVAL(WS-STOCK-STR)
018100
018200     .
018300
018400 AJOUT-OP.
018500
018600*   PASSER D'ABORD PAR LE TRANSFERT AVANT AJOUTS
018700
018800     PERFORM TRANSFERT
018900
019000*   AJOUT DANS LA BASE DE DONNEES
019100
019200
019300     EXEC SQL
019400      INSERT INTO API7.PRODUCTS
019500             (P_NO, DESCRIPTION, PRICE, STOCK)
019600      VALUES (
019700         :PRO-P-NO,
019800         :PRO-DESCRIPTION,
019900         :PRO-PRICE,
020000         :PRO-STOCK
020100         )
020200     END-EXEC
020300
020400
020500
020600     MOVE SQLCODE TO ED-SQLCODE
020700     EVALUATE TRUE
020800
020900*    CAS LORSQUE LE PRODUIT EST BIEN INSERE
021000
021100        WHEN SQLCODE = ZERO
021200             DISPLAY CODE-OP ' AJOUTE '
021300
021400*    REPERAGE DE DOUBLONS ET ECRITURE DANS LA JOURNALISATION
021500*    LOGDATA
021600
021700        WHEN SQLCODE = -803
021800             DISPLAY CODE-OP ' EXISTE DEJA ! FAILURE'
021900             MOVE SPACES TO WS-MSG
022000             MOVE SPACES TO WS-CONTEXT
022100             MOVE 1 TO WS-CONTEXT-LEN
022200             STRING 'AJOUT-NOUVEAUX-PRODUITS' DELIMITED BY SIZE
022300                    INTO WS-CONTEXT
022400                    WITH POINTER WS-CONTEXT-LEN
022500             END-STRING
022600             SUBTRACT 1 FROM WS-CONTEXT-LEN
022700
022800             STRING
022900               'ERREUR'                     DELIMITED BY SIZE
023000               WS-CONTEXT(1:WS-CONTEXT-LEN) DELIMITED BY SIZE
023100               'DOUBLON POUR ID : '         DELIMITED BY SIZE
023200               CODE-OP                      DELIMITED BY SIZE
023300               INTO WS-MSG
023400             END-STRING
023500             PERFORM LOG
023600
023700* SI CODE SQL POSITIF : AVERTISSEMENTS (WARNINGS)
023800
023900        WHEN SQLCODE > ZERO
024000             DISPLAY 'WARNING ! ' ED-SQLCODE
024100
024200             MOVE SPACES TO WS-MSG
024300             MOVE SPACES TO WS-CONTEXT
024400             MOVE 1 TO WS-CONTEXT-LEN
024500             STRING 'AJOUT-NOUVEAUX-PRODUITS' DELIMITED BY SIZE
024600                    INTO WS-CONTEXT
024700                    WITH POINTER WS-CONTEXT-LEN
024800             END-STRING
024900             SUBTRACT 1 FROM WS-CONTEXT-LEN
025000
025100             STRING
025200               'ERREUR'                     DELIMITED BY SIZE
025300               WS-CONTEXT(1:WS-CONTEXT-LEN) DELIMITED BY SIZE
025400               'ANOMALIE SQL POUR: '        DELIMITED BY SIZE
025500               CODE-OP                      DELIMITED BY SIZE
025600               INTO WS-MSG
025700             END-STRING
025800             PERFORM LOG
025900
026000*       AUTRES ERREURS SQL BLOCANTES
026100*       ANNULATION DE LA TRANSACTION EN CAS D'ERREUR GRAVE
026200
026300
026400        WHEN SQLCODE < ZERO AND SQLCODE NOT = -803
026500               DISPLAY 'ANO - SQLCODE : ' ED-SQLCODE
026600               EXEC SQL    ROLLBACK      END-EXEC
026700               PERFORM TEST-SQLCODE
026800
026900               MOVE SPACES TO WS-MSG
027000               MOVE SPACES TO WS-CONTEXT
027100               MOVE 1 TO WS-CONTEXT-LEN
027200               STRING 'AJOUT-NOUVEAUX-PRODUITS' DELIMITED BY SIZE
027300                      INTO WS-CONTEXT
027400                      WITH POINTER WS-CONTEXT-LEN
027500               END-STRING
027600               SUBTRACT 1 FROM WS-CONTEXT-LEN
027700
027800               STRING
027900                 'ERREUR'                     DELIMITED BY SIZE
028000                 WS-CONTEXT(1:WS-CONTEXT-LEN) DELIMITED BY SIZE
028100                 'ANOMALIE SQL POUR: '        DELIMITED BY SIZE
028200                 CODE-OP                      DELIMITED BY SIZE
028300                 INTO WS-MSG
028400               END-STRING
028500               PERFORM LOG
028600
028700*       SECURITE : TOUT CAS NON ANTICIPE EST TRAITE COMME ANOMALIE
028800
028900        WHEN OTHER
029000             DISPLAY 'ANO - SQLCODE : ' ED-SQLCODE
029100             EXEC SQL    ROLLBACK      END-EXEC
029200             PERFORM TEST-SQLCODE
029300
029400             MOVE SPACES TO WS-MSG
029500             MOVE SPACES TO WS-CONTEXT
029600             MOVE 1 TO WS-CONTEXT-LEN
029700             STRING 'AJOUT-NOUVEAUX-PRODUITS' DELIMITED BY SIZE
029800                    INTO WS-CONTEXT
029900                    WITH POINTER WS-CONTEXT-LEN
030000             END-STRING
030100             SUBTRACT 1 FROM WS-CONTEXT-LEN
030200
030300             STRING
030400               'ERREUR'                     DELIMITED BY SIZE
030500               WS-CONTEXT(1:WS-CONTEXT-LEN) DELIMITED BY SIZE
030600               'ANOMALIE SQL POUR: '        DELIMITED BY SIZE
030700               CODE-OP                      DELIMITED BY SIZE
030800               INTO WS-MSG
030900             END-STRING
031000             PERFORM LOG
031100
031200     END-EVALUATE
031300     .
031400
031500
031600
031700
031800**********************************************
031900*   REDACTION ERREURS TEST CODES SQL         *
032000**********************************************
032100
032200
032300 TEST-SQLCODE.
032400      MOVE SQLCODE TO ED-SQLCODE
032500      EVALUATE TRUE
032600        WHEN SQLCODE = ZERO
032700             CONTINUE
032800        WHEN SQLCODE > ZERO
032900             DISPLAY 'WARNING - ' WS-MSG-SQL ': ' ED-SQLCODE
033000        WHEN OTHER
033100             DISPLAY 'ANOMALIE - '  WS-MSG-SQL ': ' ED-SQLCODE
033200             PERFORM ABEND-PROG
033300      END-EVALUATE.
033400
033500
033600 ABEND-PROG.
033700     DISPLAY 'ANO !!!!'
033800     COMPUTE WS-ANO = 1 / WS-ANO.
033900
034000
034100**********************************************
034200*   REDACTION ERREURS DANS LE FICHIER LOG    *
034300**********************************************
034400
034500 LOG.
034600
034700* RECUPERATION DE L'UTILISATEUR COURANT
034800
034900
035000     EXEC SQL
035100            SELECT USER
035200              INTO :WS-USER
035300              FROM SYSIBM.SYSDUMMY1
035400     END-EXEC
035500
035600* APPEL DU SOUS PROGRAMME POUR REDACTION JOURNALISATION
035700
035800     CALL 'LOGDATA' USING WS-MSG, WS-PGM, WS-USER
035900       .
