000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PROJG4PP.
000300 AUTHOR. GROUP4.
000400 ENVIRONMENT DIVISION.
000500 CONFIGURATION SECTION.
000600 SPECIAL-NAMES.
000700      DECIMAL-POINT IS COMMA.
000800
000900 INPUT-OUTPUT SECTION.
001000 FILE-CONTROL.
001100     SELECT F-PROD ASSIGN TO PROD
001200     ORGANIZATION IS SEQUENTIAL
001300     FILE STATUS IS FS-PROD.
001400
001500     SELECT F-TAUX ASSIGN TO KSDS
001600     ORGANIZATION IS INDEXED
001700     ACCESS MODE IS DYNAMIC
001800     RECORD KEY IS ID-TAUX
001900     FILE STATUS IS FS-TAUX.
002000
002100     SELECT F-OUT ASSIGN TO OUT1
002200     ORGANIZATION IS SEQUENTIAL
002300     FILE STATUS IS FS-OUT.
002408
002410     SELECT F-ERR ASSIGN TO OUT2
002420     ORGANIZATION IS SEQUENTIAL
002430     FILE STATUS IS FS-ERR.
002500
002600 DATA DIVISION.
002700 FILE SECTION.
002800
002900*** DECLARATION FICHIER NEWPRODS CONTENANT LES NOUVEAU PRODUITS
003000
003100 FD  F-PROD
003200     RECORDING MODE IS F.
003300 01  ENR-PROD.
003400     05 LIGNE-PROD PIC X(50).
003500
003600*** DECLARATION FICHIER TAUX EN KSDS
003700
003800 FD  F-TAUX.
003900 01  ENR-TAUX.
004000     05 ID-TAUX PIC X(3).
004100     05 COEFF-TAUX PIC X(12).
004200     05 FILLER     PIC X(5).
004300
004400*** DECLARATION FICHIER OUT1 (NEWPRODS REFORMATE)
004500
004600 FD  F-OUT
004700     RECORDING MODE IS F.
004800 01 ENR-OUT.
004900    05 OUT-CODE-PROD PIC X(3).
005000    05 OUT-DESC-PROD PIC X(30).
005100    05 OUT-PU-USD    PIC ZZZ.ZZ.
005200    05 OUT-DEVISE    PIC X(3).
005300    05 OUT-STOCK     PIC Z(3).
005400
005416
005417*** DECLARATION FICHIER NEWPRODS.ERREUR DE SORTIE
005418*** DES ERREURS DETECTES (DOUBLONS, FORMATS ...)
005419
005420 FD  F-ERR
005421     RECORDING MODE IS F.
005422 01 ENR-ERR.
005423    05 REC-ERR       PIC X(80).
005424    05 MSG-ERR       PIC X(80).
005500
005600 WORKING-STORAGE SECTION.
005700
005800*** ZONE POUR STOCKAGE NEWPRODS APRES DECOUPAGE ***
005900
006000 01  ZONE-PRODUIT.
006100        05 CODE-PROD  PIC X(4).
006200        05 DESC-PROD  PIC X(40).
006300        05 PU-PROD    PIC X(6).
006400        05 DE-PROD    PIC X(3).
006500        05 ST-PROD    PIC X(10).
006600
006691
006700*** ZONE POUR LECTURE FICHIER ET MSG ERREURS ***
006800
006900 77 FS-TAUX           PIC 99.
007000 77 FS-PROD           PIC 99.
007100 77 FS-OUT            PIC 99.
007110 77 FS-ERR            PIC 99.
007200 77 FIN-PROD          PIC X VALUE 'N'.
007300 77 FIN-TAUX          PIC X VALUE 'N'.
007310
007400 77 FLAG-ERR-PROD     PIC X VALUE 'N'.
007500
007600
007700***     ZONE DE TRAVAIL VARIABLES TRAITEMENT LIGNE NEWPRODS ***
007800
007900** POUR LES PRIX
007910
007920
008000 77 WS-PU-STR         PIC X(10).
008100 77 WS-PU-NUM         PIC 9(5)V99.
008200 77 WS-PU-CLEAN       PIC X(10).
008210 77 WS-PU-LEN         PIC 99 VALUE ZERO.
008300 77 WS-PRIX-USD       PIC 9(5)V99.
008600 77 IDX-PU            PIC 9(2) VALUE 1.
008601
008610 77 ED-PRIX-USD       PIC 999.99.
008620 77 DEBUG-PRIX-USD    PIC ZZZZZ.ZZ.
008700 77 WS-PU-INT         PIC 9(7)V99.
008701
008702** POUR LES CODES
008703
008710 77 WS-CODE-STR       PIC X(3).
008720 77 WS-CODE-CLEAN     PIC X(4).
008721 77 WS-CODE-LEN       PIC 99 VALUE ZERO.
008722 77 WS-CODE-LAST      PIC X(4) VALUE SPACES.
008723 77 WS-DOUBLON        PIC X VALUE 'N'.
008724 77 IDX-CODE          PIC 9(2) VALUE 1.
008725
008726
008727** POUR LES DESCRIPTIONS
008728
008729 77 WS-DESC-STR       PIC X(30).
008730 77 WS-DESC-CLEAN     PIC X(40).
008731 77 WS-DESC-LEN       PIC 99 VALUE ZERO.
008734 77 IDX-DESC          PIC 9(2) VALUE 1.
008744
008745
008746
008747** POUR LES DEVISES
008748
008749 77 WS-DE-STR       PIC X(3).
008750 77 WS-DE-CLEAN     PIC X(4).
008751 77 WS-DE-LEN       PIC 99 VALUE ZERO.
008752 77 IDX-DE          PIC 9(2) VALUE 1.
008753
008754
008755*** ZONE DE TRAVAIL POUR LE FICHIER KSDS STOCKS  ***
008756
008757
008758
008759** POUR LES STOCKS
008760
008761 77 WS-ST-STR       PIC X(10).
008762 77 WS-ST-NUM       PIC 9(3).
008763 77 WS-ST-CLEAN     PIC X(10).
008764 77 WS-ST-LEN       PIC 99 VALUE ZERO.
008765 77 IDX-ST          PIC 9(2) VALUE 1.
008766 77 WS-ST-DIGITS    PIC X VALUE 'N'.
008810
008900***     ZONE DE TRAVAIL VARIABLES TRAITEMENT LIGNE TAUX     ***
009000
009100 77 WS-COEFF-STR      PIC X(12).
009400 77 WS-COEFF-NUM      PIC 9(3)V9(7).
009500 77 IDX-T             PIC 9(4) VALUE 0.
009600 77 WS-COEFF-CLEAN    PIC X(12).
009700 77 WS-COEFF-LEN      PIC 99 VALUE ZERO.
009800
010000
010600*** ZONE TRAITEMENT DESCRIPTION ***
010700
011000 77 CHAR              PIC X.
011100 77 MAJ-SUIVANTE      PIC X VALUE 'O'.
011110 77 IDX-PROD          PIC 9(4) VALUE 0.
011120 77 NB-PROD           PIC 9(02) VALUE 0.
011200
011400
011500 PROCEDURE DIVISION.
011600
011601* ============================================================
011602* PROGRAMME PRINCIPAL
011603* ===========================================================
011620* CE PROGRAMME LIT UN FICHIER DE PRODUITS, VALIDE ET NETTOIE
011630* LES DONNEES, CONVERTIT LES PRIX EN USD, ET GENERE DEUX
011640* FICHIERS : UN DE SORTIE AVEC LES PRODUITS VALIDES ET UN
011650* FICHIER D'ERREURS POUR LES ANOMALIES DéTECTéES
011660* ===========================================================
011670
011680
011700     PERFORM OUVERTURE-FICHIERS
011800     PERFORM LECTURE-FICHIERS
011900     PERFORM FERMER-FICHIERS
012000     DISPLAY '--------------------------------------'
012100     DISPLAY 'TRAITEMENT TERMINE'
012200     GOBACK.
012300
012400
012500******************************************
012600*    OUVERTURE DES FICHIERS             *
012700******************************************
012800
012900 OUVERTURE-FICHIERS.
013000
013010* OUVERTURE FICHIER NEWPRODS
013020
013100     OPEN INPUT F-PROD
013200     IF FS-PROD NOT = ZEROES THEN
013300        DISPLAY 'ERREUR OUVERTURE F-PROD : ' FS-PROD
013400        PERFORM FERMER-FICHIERS
013500     END-IF
013600
013610* OUVERTURE FICHIER KSDS TAUX
013620
013700     OPEN INPUT F-TAUX
013800     IF FS-TAUX NOT = ZEROES THEN
013900        DISPLAY 'ERREUR OUVERTURE F-TAUX : ' FS-TAUX
014000        PERFORM FERMER-FICHIERS
014100     END-IF
014200
014210* OUVERTURE DU FICHIER DE SORTIE DES PRODUITS VALIDES
014230
014300     OPEN OUTPUT F-OUT
014400     IF FS-OUT NOT = ZEROES THEN
014500        DISPLAY 'ERREUR OUVERTURE F-OUT : ' FS-OUT
014600        PERFORM FERMER-FICHIERS
014700     END-IF
014800
014810* OUVERTURE DU FICHIER DE SORTIE ERREURS
014811
014817
014818     OPEN OUTPUT F-ERR
014819     IF FS-ERR NOT = ZEROES THEN
014820        DISPLAY 'ERREUR OUVERTURE F-ERR : ' FS-ERR
014823        PERFORM FERMER-FICHIERS
014830     END-IF
014891
014900     .
015000
015100
015200
015300******************************************
015400*         LECTURE DES FICHIERS           *
015500******************************************
015600
015700
015800 LECTURE-FICHIERS.
015900
016000* INTITIALISATION DE FLAG DE FIN DE FICHIER
016010
016100     MOVE 'N' TO FIN-PROD
016200
016210* BOUCLE PRINCIPALE DE LECTURE DU FICHIER PRODUIT
016220
016300     PERFORM UNTIL FIN-PROD = 'O'
016400      READ F-PROD
016500        AT END
016501
016510*          FIN DU FICHIER ATTEINT
016520
016600           MOVE 'O' TO FIN-PROD
016700        NOT AT END
016710
016720*          COMPTEUR DE PRODUITS TRAITES
016730
016800           ADD 1 TO NB-PROD
016900
016910*          TRAITEMENT COMPLET DE LA LIGNE PRODUIT
016920
017000           PERFORM TRAITEMENT-LIGNE
017010           PERFORM AFFICHAGE
017100
017110* LECTURE DU TAUX DE CONVERSION POUR LA DEVISE DU PRODUIT
017200
017300             MOVE DE-PROD TO ID-TAUX
017400               READ F-TAUX
017500                KEY IS ID-TAUX
017600                 INVALID KEY
017610
017611*                DEVISE NON TROUVEE DANS LE FICHIER TAUX
017620
017700                    DISPLAY 'DEVISE INCONNUE : ' DE-PROD
017800                 NOT INVALID KEY
017801                    PERFORM AFFICHAGE
017802
017803*                DEVISE TROUVEE
017804
018000                    IF DE-PROD = ID-TAUX THEN
018100                    MOVE 'O' TO FIN-TAUX
018200                    END-IF
018300               END-READ
018400       END-READ
018500      END-PERFORM
018600       .
018700
018800
018900******************************************
019000*      DECOUPER LIGNE PROD SUPP ';'      *
019100******************************************
019200
019300
019400 TRAITEMENT-LIGNE.
019500
019510*     DECOUPAGE DE LA LIGNE EN 5 CHAMPS SEPARES PAR ";"
019520*     EXEMPLE : P10;USB FLASH DRIVE;15;EUR;100
019530
019540
019600      UNSTRING LIGNE-PROD
019700               DELIMITED BY ";"
019800               INTO CODE-PROD
019900                    DESC-PROD
020000                    PU-PROD
020100                    DE-PROD
020200                    ST-PROD
020300      END-UNSTRING
020310      DISPLAY 'DONNEES UNSTRING  : ' CODE-PROD
020320                                     DESC-PROD
020330                                     PU-PROD
020340                                     DE-PROD
020350                                     ST-PROD
020351      DISPLAY 'PU-PROD AVANT NETTOYAGE : ' PU-PROD
020356      DISPLAY ' LONGUEUR DE DESC-PROD : ' LENGTH OF DESC-PROD
020371
020372* NETTOYAGE ET VALIDATION DE CHAQUE CHAMPS
020373
020380      PERFORM COMPACTER-CODE
020420      PERFORM COMPACTER-PRIX
020430      PERFORM COMPACTER-DEVISE
020440      PERFORM COMPACTER-STOCK
022010      PERFORM COMPACTER-DESCRIPTION
022011
022012*     FORMATAGE DE LA DESCRIPTION (MINUSCULES, MAJUSCULES)
022013
022020      PERFORM TRAITEMENT-DESCRIPTION
022100           .
022200
022201******************************************
022202*      COMPACTER LES CODES               *
022203******************************************
022204
022205 COMPACTER-CODE.
022206
022207*    SUPPRESSION DES ESPACES DU CODE PRODUIT
022208
022209
022210     MOVE SPACES TO WS-CODE-CLEAN
022211     MOVE ZERO TO WS-CODE-LEN
022212     PERFORM VARYING IDX-CODE FROM 1 BY 1 UNTIL IDX-CODE >
022213                                   LENGTH OF CODE-PROD
022214        IF CODE-PROD(IDX-CODE:1) NOT = SPACE
022215           ADD 1 TO WS-CODE-LEN
022216           MOVE CODE-PROD(IDX-CODE:1) TO
022217                  WS-CODE-CLEAN(WS-CODE-LEN:1)
022218        END-IF
022219     END-PERFORM
022220
022221     DISPLAY '------------------ NETTOYAGE CODE ----------------'
022222     DISPLAY
022223       'PU-PROD NETTOYE : [' WS-CODE-CLEAN(1:WS-CODE-LEN) ']'
022224     DISPLAY 'LONGUEUR UTILE :' WS-CODE-LEN
022225
022226     MOVE WS-CODE-CLEAN(1:WS-CODE-LEN) TO WS-CODE-STR
022227
022228*    DETECTION DES DOUBLONS (CODE IDENTIQUE AU PRECEDENT)
022230
022231      IF WS-CODE-STR = WS-CODE-LAST
022232      MOVE 'O' TO WS-DOUBLON
022233
022234*    ENREGISTREMENT DE L'ERREUR DANS LE FICHIER NEWPRODS.ERR
022235
022236      MOVE ZONE-PRODUIT TO REC-ERR
022237        STRING
022238          'DOUBLON REPERE POUR L ID :' DELIMITED BY SIZE
022239           WS-CODE-STR                 DELIMITED BY SIZE
022240           INTO MSG-ERR
022241        WRITE ENR-ERR
022242        MOVE 'O' TO FLAG-ERR-PROD
022243      ELSE
022244
022245*     PAS DE DOUBLON, MEMORISATION DU CODE
022246
022247        MOVE 'N' TO WS-DOUBLON
022248        MOVE WS-CODE-STR TO WS-CODE-LAST
022249      END-IF
022250     .
022251
022252******************************************
022253*      COMPACTER LES DEVISES             *
022254******************************************
022255
022256 COMPACTER-DEVISE.
022257
022258*    SUPPRESSION DES ESPACES DES DEVISES
022259
022260
022261     MOVE SPACES TO WS-DE-CLEAN
022262     MOVE ZERO TO WS-DE-LEN
022263     PERFORM VARYING IDX-DE FROM 1 BY 1 UNTIL IDX-DE >
022264                                   LENGTH OF DE-PROD
022265        IF DE-PROD(IDX-CODE:1) NOT = SPACE
022266           ADD 1 TO WS-DE-LEN
022267           MOVE DE-PROD(IDX-DE:1) TO
022268                  WS-DE-CLEAN(WS-DE-LEN:1)
022269        END-IF
022270     END-PERFORM
022271     DISPLAY '------------------ NETTOYAGE DEVISES--------------'
022272     DISPLAY
022273       'PU-PROD NETTOYE : [' WS-DE-CLEAN(1:WS-CODE-LEN) ']'
022274     DISPLAY 'LONGUEUR UTILE :' WS-DE-LEN
022275     MOVE WS-DE-CLEAN(1:WS-DE-LEN) TO WS-DE-STR
022276     .
022277
022278******************************************
022279*      COMPACTER LES DESCRIPTIONS       *
022280******************************************
022281
022282 COMPACTER-DESCRIPTION.
022283
022284*   SUPPRESSION DES ESPACES DE FIN DES DESCRIPTIONS
022285
022286
022287     MOVE SPACES TO WS-DESC-CLEAN
022288     MOVE ZERO TO WS-DESC-LEN
022289
022290* RECHERCHE DU DERNIER CARACTERE NON-ESPACE
022291
022292     PERFORM VARYING IDX-DESC FROM LENGTH OF DESC-PROD
022293                              BY -1 UNTIL IDX-DESC < 1
022294                              OR WS-DESC-LEN > 0
022295        IF DESC-PROD(IDX-DESC:1) NOT = SPACE
022296           MOVE IDX-DESC TO WS-DESC-LEN
022297        END-IF
022298     END-PERFORM
022299
022300     MOVE DESC-PROD(1:WS-DESC-LEN) TO WS-DESC-CLEAN
022303
022304     DISPLAY '---------- NETTOYAGE DESCRIPTION --------------'
022305     DISPLAY
022306      'DESC-PROD NETTOYE : [' WS-DESC-CLEAN(1:WS-DESC-LEN) ']'
022307     DISPLAY 'LONGUEUR UTILE :' WS-DESC-LEN
022308
022309*    VALIDATION DE LA LONGUEUR (MAX 30 CARACTERES)
022310
022311     IF WS-DESC-LEN > 30
022312
022313*    DESCRIPTION TROP LONGUE : ENREGISTREMENT ERREUR
022314
022315        MOVE ZONE-PRODUIT TO REC-ERR
022320
022321        STRING
022322          'CHAMPS TROP LONG (SUPERIEUR A : ' DELIMITED BY SIZE
022323            ' 30 '                            DELIMITED BY SIZE
022324            ' CHARS ) '                       DELIMITED BY SIZE
022325            DESC-PROD(1:WS-DESC-LEN)          DELIMITED BY SIZE
022326           INTO MSG-ERR
022327        WRITE ENR-ERR
022328        MOVE 'O' TO FLAG-ERR-PROD
022329     ELSE
022330
022331*    DESCRIPTION VALIDE : MISE A JOUR
022332
022333        MOVE SPACES TO DESC-PROD
022334        MOVE WS-DESC-CLEAN(1:WS-DESC-LEN) TO DESC-PROD
022335        MOVE WS-DESC-CLEAN(1:WS-DESC-LEN) TO WS-DESC-STR
022336     END-IF
022340     .
022409
022410
022411
022412******************************************
022413*      COMPACTER LES PRIX                *
022414******************************************
022415
022416 COMPACTER-PRIX.
022417
022418*    SUPPRESSION DES ESPACES DU PRIX
022419
022420     MOVE SPACES TO WS-PU-CLEAN
022421     MOVE ZERO TO WS-PU-LEN
022422     PERFORM VARYING IDX-PU FROM 1 BY 1 UNTIL IDX-PU >
022430                                   LENGTH OF PU-PROD
022440        IF PU-PROD(IDX-PU:1) NOT = SPACE
022450           ADD 1 TO WS-PU-LEN
022460           MOVE PU-PROD(IDX-PU:1) TO WS-PU-CLEAN(WS-PU-LEN:1)
022470        END-IF
022480     END-PERFORM
022481     DISPLAY '------------------ NETTOYAGE PRIX ----------------'
022490     DISPLAY 'PU-PROD NETTOYE : [' WS-PU-CLEAN(1:WS-PU-LEN) ']'
022491     DISPLAY 'LONGUEUR UTILE :' WS-PU-LEN
022492     MOVE WS-PU-CLEAN(1:WS-PU-LEN) TO WS-PU-STR
022516
022517*    REMPLACEMENT DU POINT PAR LA VIRGULE (FORMAT DECIMALE)
022518
022519     DISPLAY 'WS-PU-STR AVANT LE REPLACE : ' WS-PU-STR
022520     INSPECT WS-PU-STR REPLACING ALL '.' BY ','
022521     DISPLAY 'WS-PU-STR APRèS LE REPLACE : ' WS-PU-STR
022522     DISPLAY 'VALEUR DE WS-PU-STR :  [' WS-PU-STR ']'
022523     DISPLAY ' WS-PU-LEN = ' WS-PU-LEN
022524
022525*    VALIDATION DU FORMAT NUMERIQUE
022526
022527     IF WS-PU-STR(1:1) >= '0' AND WS-PU-STR (1:1) <= '9'
022528
022529*    CONVERSION EN NUMERIQUE ET MULTIPLICATION PAR 100
022530
022531        COMPUTE WS-PU-NUM = FUNCTION NUMVAL(WS-PU-STR)
022540        COMPUTE WS-PU-NUM = WS-PU-NUM * 100
022541     ELSE
022542
022543*    FORMAT INVALIDE : ENREGISTREMENT ERREUR
022544
022545
022553        MOVE ZONE-PRODUIT TO REC-ERR
022554        STRING
022555          'MAUVAIS FORMAT (NON NUMERIQUE)   :' DELIMITED BY SIZE
022556            WS-PU-STR                          DELIMITED BY SIZE
022557           INTO MSG-ERR
022558        WRITE ENR-ERR
022559        MOVE 0 TO WS-PU-NUM
022560        MOVE 'O' TO FLAG-ERR-PROD
022561     END-IF
022562        DISPLAY ' WS-PU-NUM : [' WS-PU-NUM ']'
022563        DISPLAY ' VARIABLE AVANT CONV NUMERIQUE : ' WS-PU-STR
022564        DISPLAY ' VARIABLE CONVERTIE EN NUMERIQUE :' WS-PU-NUM
022565        DISPLAY ' ---------------------------------------------- '
022566     .
022570
022630
022631******************************************
022632*      COMPACTER LES STOCKS             *
022633******************************************
022634
022635* SUPPRESSION DES ESPACES DU STOCK
022636
022637
022638 COMPACTER-STOCK.
022639
022640     MOVE SPACES TO WS-ST-CLEAN
022641     MOVE ZERO TO WS-ST-LEN
022642     PERFORM VARYING IDX-ST FROM 1 BY 1 UNTIL IDX-ST >
022643                                   LENGTH OF ST-PROD
022644           IF ST-PROD(IDX-ST:1) NOT = SPACE
022645           ADD 1 TO WS-ST-LEN
022646           MOVE ST-PROD(IDX-ST:1) TO
022647                  WS-ST-CLEAN(WS-ST-LEN:1)
022648        END-IF
022649     END-PERFORM
022650     DISPLAY '------------------ NETTOYAGE STOCK----------------'
022651     DISPLAY
022652       'ST-PROD NETTOYE : [' WS-ST-CLEAN(1:WS-ST-LEN) ']'
022653     DISPLAY 'LONGUEUR UTILE :' WS-ST-LEN
022654     MOVE WS-ST-CLEAN(1:WS-ST-LEN) TO WS-ST-STR
022680
022681*    VERIFIER QUE TOUS LES CARACTRES SOIENT BIEN DES CHIFFRES
022683
022684     MOVE 'O' TO WS-ST-DIGITS
022685
022686     PERFORM VARYING IDX-ST FROM 1 BY 1 UNTIL
022687                     IDX-ST > WS-ST-LEN
022688
022689     IF WS-ST-STR(IDX-ST:1) < '0' OR
022690           WS-ST-STR(IDX-ST:1) > '9'
022691
022692*    CARACTERE NON NUMERIQUE DETECTE
022693
022695           MOVE 'N' TO WS-ST-DIGITS
022696     END-IF
022697     END-PERFORM
022698
022699
022700     IF WS-ST-DIGITS = 'O'
022701
022702*    STOCK VALIDE : CONVERSION EN NUMERIQUE
022703
022704          COMPUTE WS-ST-NUM = FUNCTION NUMVAL(WS-ST-STR)
022705     ELSE
022706
022707*    STOCK INVALIDE : ENREGISTREMENT ERREUR
022708
022720          MOVE ZONE-PRODUIT TO REC-ERR
022721        STRING
022722            'MAUVAIS FORMAT (NON NUMERIQUE) :' DELIMITED BY SIZE
022723            WS-ST-STR DELIMITED BY SIZE
022724            INTO MSG-ERR
022725        END-STRING
022726        WRITE ENR-ERR
022727        MOVE 0 TO WS-ST-NUM
022728        MOVE 'O' TO FLAG-ERR-PROD
022729     END-IF
022730
022731
022732
022735     .
022800
024400
024500******************************************
024600*    TRAITEMENT CARACTERES DESCRIPTION  *
024700******************************************
024800
024900
025000 TRAITEMENT-DESCRIPTION.
025100
025110*    MISE EN MINUSCULE DE TOUTE LA DESCRIPTION
025130
025200     MOVE FUNCTION LOWER-CASE(WS-DESC-STR) TO WS-DESC-STR
025300     MOVE 1 TO IDX-PROD
025400     MOVE 'O' TO MAJ-SUIVANTE
025500
025510*    BOUCLE POUR METTRE EN MAJUSCULE LA PREMIERE LETTRE
025520*    DE CHAQUE MOT (APRES ESPACE OU TIRET)
025530
025600     PERFORM VARYING IDX-PROD FROM 1 BY 1
025700             UNTIL IDX-PROD > LENGTH OF WS-DESC-STR
025800
025900     MOVE WS-DESC-STR(IDX-PROD:1) TO CHAR
026000
026100      EVALUATE TRUE
026200       WHEN MAJ-SUIVANTE = 'O'
026300            AND CHAR NOT = SPACE
026400            AND CHAR NOT = '-'
026410
026420*           PERMIER CARACTERE D'UN MOT : MISE EN MAJUSCULE
026430
026500             MOVE FUNCTION UPPER-CASE(CHAR)
026600                           TO WS-DESC-STR(IDX-PROD:1)
026700
026800             MOVE 'N' TO MAJ-SUIVANTE
026900
027000       WHEN MAJ-SUIVANTE = 'O'
027100            AND (CHAR = SPACE OR CHAR = '-')
027110
027120*           ESPACE OU TIRET AVANT LE PREMIER CARACTERE
027130
027200            CONTINUE
027300
027400       WHEN MAJ-SUIVANTE = 'N'
027500            AND (CHAR = SPACE OR CHAR = '-')
027510
027520*           SEPARATEUR TROUVE : LE PROCHAIN CARACTERE SERA EN MAJ
027530
027600            MOVE 'O' TO MAJ-SUIVANTE
027700
027800       WHEN OTHER
027810
027820*           AUTRES CAS : ON CONTINUE NORMALEMENT
027830
027900            CONTINUE
028000
028100      END-EVALUATE
028200     END-PERFORM
028300
028310* LECTURE DU TAUX DE CONVERSION POUR CETTE DEVISE
028320
028330
028400     MOVE DE-PROD TO ID-TAUX
028500     READ F-TAUX KEY IS ID-TAUX
028600       INVALID KEY
028610
028620*        DEVISE NON TROUVEE : COEFFICIENT PAR DEFAUT = 1
028630
028700         DISPLAY 'DEVISE INCONNUE : ' DE-PROD
028800         MOVE 1 TO COEFF-TAUX
028900       NOT INVALID KEY
028901
028902*      DEVISE TROUVEE : TRAITEMENT DU COEFFICIENT
028903
028910         PERFORM COMPACTER-TAUX
029000         PERFORM TRAITER-TAUX
029100     END-READ
029101
029102*    ECRITURE DU PRODUIT SI AUCUNE ERREUR N'A ETE DETECTE
029103
029110     IF FLAG-ERR-PROD = 'N' THEN
029200        PERFORM ECRITURE-SORTIE
029210     ELSE
029211
029212*    PRODUIT EN ERREUR : NON ECRIT DANS LE FICHIER DE SORTIE
029213
029220        DISPLAY 'PRODUIT NON ECRIT SUITE ERREUR : ' CODE-PROD
029230     END-IF
029231
029232*    REINITIALISATION DU FLAG D'ERREUR POUR LE PROCHAIN PRODUIT
029233
029240     MOVE 'N' TO FLAG-ERR-PROD
029300     .
029400
029401
029402******************************************
029403*      COMPACTER LES TAUX DES DEVISES    *
029404******************************************
029411
029412 COMPACTER-TAUX.
029413
029414*    SUPPRESSION DES ESPACES DU COEFFICIENT
029415
029416     MOVE SPACES TO WS-COEFF-CLEAN
029417     MOVE ZERO TO WS-COEFF-LEN
029418     PERFORM VARYING IDX-T FROM 1 BY 1 UNTIL IDX-T >
029419                                   LENGTH OF COEFF-TAUX
029420        IF COEFF-TAUX(IDX-T:1) NOT = SPACE
029421           ADD 1 TO WS-COEFF-LEN
029422           MOVE COEFF-TAUX(IDX-T:1) TO
029423             WS-COEFF-CLEAN(WS-COEFF-LEN:1)
029424
029425        END-IF
029426
029427     END-PERFORM
029428     DISPLAY '----------------COEFF TRAITEMENT ------------'
029429     DISPLAY 'TAUX NETTOYE : [' WS-COEFF-CLEAN(WS-COEFF-LEN:1) ']'
029430     DISPLAY 'LONGUEUR UTILE :' WS-COEFF-LEN
029431
029432     MOVE WS-COEFF-CLEAN(1:WS-COEFF-LEN) TO WS-COEFF-STR
029433
029434*    REMPLACEMENT DU POINT PAR LA VIRGULE (FORMAT DECIMALE)
029435
029436
029437     DISPLAY 'WS-PU-STR AVANT LE REPLACE : ' WS-COEFF-STR
029438     INSPECT WS-COEFF-STR REPLACING ALL '.' BY ','
029439     DISPLAY 'WS-PU-STR APRèS LE REPLACE : ' WS-COEFF-STR
029440
029441*    VALIDATION ET CONVERSION DU COEFFICIENT
029442
029443     IF WS-COEFF-STR(1:1) >= '0' AND WS-COEFF-STR (1:1) <= '9'
029444        COMPUTE WS-COEFF-NUM = FUNCTION NUMVAL(WS-COEFF-STR)
029450     ELSE
029451
029452*    FORMAT INVALIDE : ENREGISTREMENT ERREUR
029453
029454      MOVE ZONE-PRODUIT TO REC-ERR
029455        STRING
029456          'MAUVAIS FORMAT (NON NUMERIQUE) :' DELIMITED BY SIZE
029457            WS-COEFF-STR                     DELIMITED BY SIZE
029458           INTO MSG-ERR
029459        WRITE ENR-ERR
029460        MOVE 0 TO WS-COEFF-NUM
029461        MOVE 'O' TO FLAG-ERR-PROD
029462     END-IF
029463        DISPLAY ' VARIABLE AVANT CONV NUMERIQUE : ' WS-COEFF-STR
029464        DISPLAY ' VARIABLE CONVERTIE EN NUMERIQUE :' WS-COEFF-NUM
029465        DISPLAY '----------------------------------------------'
029466     .
029467
029468
029470
029500
029600******************************************
029700*   TRAITEMENT DES TAUX ET CONVERSION    *
029800******************************************
029810
029900 TRAITER-TAUX.
030000
030010*    CALCUL DU PRIX EN USD : PRIX ORIGINE * COEFFCIENT
030020*    EXEMPLE : 45.99 EUR * 1.1578 = 53,24 USD
030050
030400     COMPUTE WS-PRIX-USD = WS-PU-NUM * WS-COEFF-NUM
030500     MOVE WS-PRIX-USD TO WS-PU-INT
030501     MOVE WS-PU-INT TO DEBUG-PRIX-USD
030510     DISPLAY ' VALEUR EXACTE USD : [' DEBUG-PRIX-USD ']'
030520     DISPLAY ' DEBUG-PRIX-USD : ' DEBUG-PRIX-USD
030600     DISPLAY '---------CONVERSION EFFECTUEE :---------- '
030700     DISPLAY '  DEVISE : ' DE-PROD
030710     DISPLAY 'COEFF TAUX : ' WS-COEFF-NUM
030800     DISPLAY '  PRIX ORIGINE : ' WS-PU-NUM
031000     DISPLAY '  PRIX USD : ' WS-PRIX-USD
031010     DISPLAY ' ---------------------------------------- '
031100     .
031200
031300
031400
031500******************************************
031600*       ECRITURE SORTIE SUR F-OUT        *
031700******************************************
031800
031900 ECRITURE-SORTIE.
032120
032130*          PREPARATION DE L'ENREGISTREMENT DE SORTIE
032160
032200           MOVE WS-CODE-LAST     TO OUT-CODE-PROD
032300           MOVE WS-DESC-STR      TO OUT-DESC-PROD
032400           MOVE DEBUG-PRIX-USD   TO OUT-PU-USD
032500           MOVE 'USD'            TO OUT-DEVISE
032600           MOVE WS-ST-NUM        TO OUT-STOCK
032610
032620*          ECRITURE DNAS LE FICHIER DE SORTIE OUTPRODS.DATA
032630
032700           WRITE ENR-OUT
033000           .
033100
033200
033300******************************************
033400*             AFFICHAGE                  *
033500******************************************
033600
033700
033800 AFFICHAGE.
033900
033910
033920*       AFFICHAGE DU TRAITEMENT (DEBUG)
033930
034000        DISPLAY 'LECTURE PROD AVANT TRAITEMENT' LIGNE-PROD
034100        DISPLAY 'LECTURE APRES TRAITEMENT: ' ZONE-PRODUIT
034200        DISPLAY 'LECTURE TAUX : ' ENR-TAUX
034300        DISPLAY '----------------------------------'
034400       .
034500
034501
034601
034610******************************************
034700*        FERMETURES FICHIERS             *
034800******************************************
034900
035000 FERMER-FICHIERS.
035100
035110*      FERMETURE DE TOUS LES FICHIERS OUVERTS
035120
035200       CLOSE F-PROD
035300             F-TAUX
035400             F-OUT
035410             F-ERR
035500        .
